plugins {
    id 'java'
}

group = 'me.nacharon'
version = plugin_version

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "enginehub"
        url = uri("https://maven.enginehub.org/repo/")
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${minecraft_version}-R0.1-SNAPSHOT"

    implementation platform("com.intellectualsites.bom:bom-newest:${bom_version}")
    // Centralisation de la version du BOM
    // Ref: https://github.com/IntellectualSites/bom
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")
}

def javaVersion = java_version.toInteger()

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    targetCompatibility = JavaVersion.toVersion(javaVersion)

    if (JavaVersion.current() < JavaVersion.toVersion(javaVersion)) {
        toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (javaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(javaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'

    // Désactive le filtrage si IDEA pose problème
    if (!System.getProperty("idea.active")) {
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

